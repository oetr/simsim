; Copyright (C) 2003,2006 B. Poettering
; Adapted to avr-gcc and simsim by Peter Samarin
; 
; This program is free software; you can redistribute and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version. Whenever you redistribute a copy
; of this document, make sure to include the copyright and license
; agreement without modification.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
; The license text can be found here: http://www.gnu.org/licenses/gpl.txt

;                http://point-at-infinity.org/avraes/
;
; This AES implementation was written in May 2003 by B. Poettering. It is 
; published under the terms of the GNU General Public License. If you need 
; AES code, but this license is unsuitable for your project, feel free to 
; contact me: avraes AT point-at-infinity.org


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;                                 RijndaelFast
;
; This is a microcontroller implementation of the Rijndael block cipher, better
; known as AES. The target device class is Atmel's AVR, a family of very fast 
; and very powerful flash MCUs, operating at clock rates up to 16 MHz,  
; executing one instruction per clock cycle (16 MIPS). The implementation 
; given here is optimized for speed (versus codesize), and achieves an 
; encryption rate of more than 100 kByte per second (on a 16MHz MCU). 
; The decryption performs about 40% slower than encryption (typical for 
; Rijndael).
; 
; The implemented algorithm is restricted to block and key sizes of 128 bit. 
; Larger key sizes can be obtained by altering the key scheduling code, which
; should be easy. As the cipher's state is completely kept in registers
; (which are limited in number), the block size is not that easy to enlarge.
; 
; This implementation makes extensive use of the AVR's "lpm" instruction,
; which loads data bytes from program memory at given addresses (the s-boxes 
; are realized that way). Some members of the AVR family don't offer that 
; instruction at all (e.g. AT90S1200), others only in a restricted way 
; (forcing the target register to be r0). The code below requires the least 
; restricted lpm instruction (with free choice of the target register).
; The ATmega161 devices meet the above mentioned requirements.
; 
; Statistics:
; 
; 16 MHz MCU | clock cycles | blocks per second | bytes per second
; -----------+--------------+-------------------+------------------
; encryption |    2474      |       6467        |      103476
; decryption |    3411      |       4691        |       75051
; 
; KEY SETUP TIME
; encryption: 756 clock cycles
; decryption: 756 + 4221 = 4977 clock cycles
; 
; CODE SIZE
; instructions: 1306 byte ( 653 words)
; sboxes:       1792 byte ( 896 words) = 7 * 256 byte
; total:        3098 byte (1549 words)
;
; RAM REQUIREMENTS
; 16 * 11 = 176 byte for each expanded key
;
;
; This source code consists of four routines and an example application, 
; which encrypts a certain plaintext and decrypts it afterwards with the
; same key. Comments in the code clarify the interaction between the key 
; expansion and the encryption/decryption routines.
;
; I encourage to read the following Rijndael-related papers/books/sites:
; [1] "The Design of Rijndael", Daemen & Rijmen, Springer, ISBN 3-540-42580-2
; [2] http://www.esat.kuleuven.ac.be/~rijmen/rijndael/
; [3] http://www.esat.kuleuven.ac.be/~rijmen/rijndael/rijndaeldocV2.zip
; [4] http://www.esat.kuleuven.ac.be/~rijmen/rijndael/atmal.zip
; [5] http://csrc.nist.gov/CryptoToolkit/aes/rijndael/
;
; [1] is *the* book about Rijndael, [2] is the official Rijndael homepage,
; [3] contains the complete Rijndael AES specification, [4] is another
; Rijndael-implementation for AVR MCUs (but much slower than this one, 
; taking 3815 clock cycles per encryption), [5] is the official NIST AES 
; site with further links.
;
; AVR and ATmega are registered trademarks by the ATMEL corporation.
; See http://www.atmel.com and http://www.atmel.com/products/avr/ for
; further details.

	
	
;;; ***************************************************************************
;;; The Rijndael cipher acts on a so-called (128 bit) "state matrix", 
;;; represented here by the 4x4 state bytes ST11-ST44. To guarantee maximum
;;; performance on AVR MCUs, these bytes are kept in registers (defaulted to
;;; the 16 low order registers r0-r15, but this may be changed if required).
;;; 
;;; The implementation makes use of six auxiliary registers (H1-H5 and I),
;;; some of which must reside in the upper registers (r16-r31). In addition
;;; ramp-registers YH:YL and ZH:ZL are used.
;;;
;;; If the context *really* requires more registers than the remaining ones, 
;;; it seems promising to move the I-register to a (fixed) ram location. 
;;; In the time crititcal routines the I-value is rarely used, thus the 
;;; speed loss obtained by dropping it from the register file is acceptible. 

ST11=0
ST21=1
ST31=2
ST41=3
ST12=4
ST22=5
ST32=6
ST42=7
ST13=8
ST23=9
ST33=10
ST43=11
ST14=12
ST24=13
ST34=14
ST44=15
H1=16
H2=17
H3=18
H4=19
H5=20
I=21
SPH=0x003E
SPL=0x003D

.set RAMEND,0x045f

main:
	cli			; initialize stack
	ldi r31,hi8(RAMEND)
	out SPH,r31
	ldi r31,lo8(RAMEND)
	out SPL,r31

	ldi ZH, hi8(key)	; load key into ST11-ST44
	ldi ZL, lo8(key)
	lpm ST11, Z+
	lpm ST21, Z+
	lpm ST31, Z+
	lpm ST41, Z+
	lpm ST12, Z+
	lpm ST22, Z+
	lpm ST32, Z+
	lpm ST42, Z+
	lpm ST13, Z+
	lpm ST23, Z+
	lpm ST33, Z+
	lpm ST43, Z+
	lpm ST14, Z+
	lpm ST24, Z+
	lpm ST34, Z+
	lpm ST44, Z+
	
	ldi YH, 0x00		; expand key to the memory
	ldi YL, 0x60		; locations $60..$60+(16*11-1)
	rcall key_expand

	ldi ZH, hi8(plaintext)	; load plaintext into ST11-ST44
	ldi ZL, lo8(plaintext)
	lpm ST11, Z+
	lpm ST21, Z+
	lpm ST31, Z+
	lpm ST41, Z+
	lpm ST12, Z+
	lpm ST22, Z+
	lpm ST32, Z+
	lpm ST42, Z+
	lpm ST13, Z+
	lpm ST23, Z+
	lpm ST33, Z+
	lpm ST43, Z+
	lpm ST14, Z+
	lpm ST24, Z+
	lpm ST34, Z+
	lpm ST44, Z+

	; now the registers ST11-ST44 contain the plaintext given below
	
	ldi YH, 0		; initialize YH:YL to
	ldi YL, 0x60		; expanded key and call
	rcall encrypt		; encryption routine
encrypted:	rjmp encrypted

	
plaintext:
.byte 0xff,0x00,0xff,0x00,0xff,0x00,0xff,0x00,0xff,0x00,0xff,0x00,0xff,0x00,0xff,0x00

key:
.byte 0x2b,0x7e,0x15,0x16,0x28,0xae,0xd2,0xa6,0xab,0xf7,0x15,0x88,0x09,0xcf,0x4f,0x3c


;;; ***************************************************************************
;;; 
;;; KEY_EXPAND
;;; The following routine implements the Rijndael key expansion algorithm. The 
;;; caller supplies the 128 bit key in the registers ST11-ST44 and a pointer 
;;; in the YH:YL register pair. The key is expanded to the memory 
;;; positions [Y : Y+16*11-1]. Note: the key expansion is necessary for both
;;; encryption and decryption.
;;; 
;;; Parameters:
;;;     ST11-ST44:	the 128 bit key
;;;         YH:YL:	pointer to ram location
;;; Touched registers:
;;;     ST11-ST44,H1-H3,ZH,ZL,YH,YL
;;; Clock cycles:	756

key_expand:
	ldi H1, 1
	ldi H2, 0x1b
	ldi ZH, hi8(sbox)
	rjmp keyexp1
keyexp0:mov ZL, ST24
	lpm H3, Z
	eor ST11, H3
	eor ST11, H1
	mov ZL, ST34
	lpm H3, Z
	eor ST21, H3
	mov ZL, ST44
	lpm H3, Z
	eor ST31, H3
	mov ZL, ST14
	lpm H3, Z
	eor ST41, H3
	eor ST12, ST11
	eor ST22, ST21
	eor ST32, ST31
	eor ST42, ST41
	eor ST13, ST12
	eor ST23, ST22
	eor ST33, ST32
	eor ST43, ST42
	eor ST14, ST13
	eor ST24, ST23
	eor ST34, ST33
	eor ST44, ST43
	lsl H1
	brcc keyexp1
	eor H1, H2
keyexp1:st Y+, ST11
	st Y+, ST21
	st Y+, ST31
	st Y+, ST41
	st Y+, ST12
	st Y+, ST22
	st Y+, ST32
	st Y+, ST42
	st Y+, ST13
	st Y+, ST23
	st Y+, ST33
	st Y+, ST43
	st Y+, ST14
	st Y+, ST24
	st Y+, ST34
	st Y+, ST44
	cpi H1, 0x6c
	brne keyexp0
	ret

;;; ***************************************************************************
;;; 
;;; PATCH_DECRYPTION_KEY
;;; The following routine applies the MixColumns diffusion operator to the 
;;; columns of the expanded key (to be precise:	to all but the first and
;;; last four). This is necessary, as the decryption routine below implements
;;; the so-called "equivalent decryption algorithm" of Rijndael.The original 
;;; key material is overwritten by the patched one. 
;;; Note: this routine is only needed for decryption purposes!
;;; 
;;; Parameters:
;;;         YH:YL:	pointer to expanded key
;;; Touched registers:
;;;     ST11-ST41,H1-H5,I,ZH,ZL,YH,YL
;;; Clock cycles:	4221

patch_decryption_key:
	adiw YL, 16
	ldi I, 35
patchd0:ldd ST11, Y+0
	ldd ST21, Y+1
	ldd ST31, Y+2
	ldd ST41, Y+3
	ldi ZH, hi8(sbox)
	mov ZL, ST11
	lpm ZL, Z
	ldi ZH, hi8(isbox0e)
	lpm H1, Z
	ldi ZH, hi8(isbox09)
	lpm H2, Z
	ldi ZH, hi8(isbox0d)
	lpm H3, Z
	ldi ZH, hi8(isbox0b)
	lpm H4, Z
	ldi ZH, hi8(sbox)
	mov ZL, ST21
	lpm ZL, Z
	ldi ZH, hi8(isbox0b)
	lpm H5, Z
	eor H1, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor H2, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H3, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H4, H5
	ldi ZH, hi8(sbox)
	mov ZL, ST31
	lpm ZL, Z
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H1, H5
	ldi ZH, hi8(isbox0b)
	lpm H5, Z
	eor H2, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor H3, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H4, H5
	ldi ZH, hi8(sbox)
	mov ZL, ST41
	lpm ZL, Z
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H1, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H2, H5
	ldi ZH, hi8(isbox0b)
	lpm H5, Z
	eor H3, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor H4, H5
	st Y+, H1
	st Y+, H2
	st Y+, H3
	st Y+, H4
	dec I
	sbrs I, 7
	jmp patchd0
	ret

;;; ***************************************************************************
;;; 
;;; ENCRYPT 
;;; This routine encrypts a 128 bit plaintext block (supplied in ST11-ST44), 
;;; using an expanded key given in YH:YL. The resulting 128 bit ciphertext
;;; block is stored in ST11-ST44.
;;;
;;; Parameters:
;;;         YH:YL:	pointer to expanded key
;;;         ST11-ST44:  128 bit plaintext block
;;; Touched registers:
;;;     ST11-ST41,H1-H5,I,ZH,ZL,YH,YL
;;; Clock cycles:	2474
		
encrypt:
	rcall encryp1
	ldi ZH, hi8(sbox)
	ldi I, 8
encryp0:mov ZL, ST11		; 1
	lpm H2, Z
	mov H3, H2
	mov H4, H2
	ldi ZH, hi8(sbox02)
	lpm H1, Z
	eor H4, H1
	mov ZL, ST22
	lpm H5, Z
	eor H1, H5
	eor H2, H5
	ldi ZH, hi8(sbox)
	lpm H5, Z
	eor H1, H5
	eor H3, H5
	eor H4, H5
	mov ZL, ST33
	lpm H5, Z
	eor H1, H5
	eor H2, H5
	eor H4, H5
	ldi ZH, hi8(sbox02)
	lpm H5, Z
	eor H2, H5
	eor H3, H5
	mov ZL, ST44
	lpm H5, Z
	eor H3, H5
	eor H4, H5
	ldi ZH, hi8(sbox)
	lpm H5, Z
	eor H1, H5
	eor H2, H5
	eor H3, H5
	ldd ST11, Y+0
	eor ST11, H1
	mov ZL, ST41		; 2
	ldd ST41, Y+3
	eor ST41, H4
	lpm H1, Z
	mov H4, H1
	mov ST33, H1
	ldi ZH, hi8(sbox02)
	lpm ST44, Z
	eor ST33, ST44
	mov ZL, ST12
	lpm H5, Z
	eor H1, H5
	eor ST44, H5
	ldi ZH, hi8(sbox)
	lpm H5, Z
	eor H4, H5
	eor ST33, H5
	eor ST44, H5
	mov ZL, ST23
	lpm H5, Z
	eor H1, H5
	eor ST33, H5
	eor ST44, H5
	ldi ZH, hi8(sbox02)
	lpm H5, Z
	eor H1, H5
	eor H4, H5
	mov ZL, ST34
	lpm H5, Z
	eor H4, H5
	eor ST33, H5
	ldi ZH, hi8(sbox)
	lpm H5, Z
	eor H1, H5
	eor H4, H5
	eor ST44, H5
	ldd ST12, Y+4
	eor ST12, H1
	ldd ST22, Y+5
	eor ST22, H4
	mov ZL, ST31		; 3
	ldd ST31, Y+2
	eor ST31, H3
	lpm ST34, Z
	mov H3, ST34
	mov H1, ST34
	ldi ZH, hi8(sbox02)
	lpm H4, Z
	eor H3, H4
	mov ZL, ST42
	ldd ST42, Y+7
	eor ST42, ST44
	lpm H5, Z
	eor H4, H5
	eor H1, H5
	ldi ZH, hi8(sbox)
	lpm H5, Z
	eor ST34, H5
	eor H3, H5
	eor H4, H5
	mov ZL, ST13
	lpm H5, Z
	eor H3, H5
	eor H4, H5
	eor H1, H5
	ldi ZH, hi8(sbox02)
	lpm H5, Z
	eor ST34, H5
	eor H1, H5
	mov ZL, ST24
	lpm H5, Z
	eor ST34, H5
	eor H3, H5
	ldi ZH, hi8(sbox)
	lpm H5, Z
	eor ST34, H5
	eor H4, H5
	eor H1, H5
	ldd ST13, Y+8
	eor ST13, ST34
	ldd ST23, Y+9
	eor ST23, H3
	mov ZL, ST32		; 4
	ldd ST32, Y+6
	eor ST32, ST33
	ldd ST33, Y+10
	eor ST33, H4
	lpm ST24, Z
	mov ST34, ST24
	mov H4, ST24
	ldi ZH, hi8(sbox02)
	lpm H3, Z
	eor ST34, H3
	mov ZL, ST43
	ldd ST43, Y+11
	eor ST43, H1
	lpm H5, Z
	eor H3, H5
	eor H4, H5
	ldi ZH, hi8(sbox)
	lpm H5, Z
	eor ST24, H5
	eor ST34, H5
	eor H3, H5
	mov ZL, ST14
	lpm H5, Z
	eor ST34, H5
	eor H3, H5
	eor H4, H5
	ldi ZH, hi8(sbox02)
	lpm H5, Z
	eor ST24, H5
	eor H4, H5
	mov ZL, ST21
	lpm H5, Z
	eor ST24, H5
	eor ST34, H5
	ldi ZH, hi8(sbox)
	lpm H5, Z
	eor ST24, H5
	eor H3, H5
	eor H4, H5
	ldd ST21, Y+1
	eor ST21, H2
	ldd ST14, Y+12
	eor ST14, ST24
	ldd ST24, Y+13
	eor ST24, ST34
	ldd ST34, Y+14
	eor ST34, H3
	ldd ST44, Y+15
	eor ST44, H4
	adiw YL, 16
	dec I
	sbrs I,7
	jmp encryp0
	mov ZL, ST11
	lpm ST11, Z
	mov ZL, ST12
	lpm ST12, Z
	mov ZL, ST13
	lpm ST13, Z
	mov ZL, ST14
	lpm ST14, Z
	mov H1, ST21
	mov ZL, ST22
	lpm ST21, Z
	mov ZL, ST23
	lpm ST22, Z
	mov ZL, ST24
	lpm ST23, Z
	mov ZL, H1
	lpm ST24, Z
	mov H1, ST31
	mov ZL, ST33
	lpm ST31, Z
	mov ZL, H1
	lpm ST33, Z
	mov H1, ST32
	mov ZL, ST34
	lpm ST32, Z
	mov ZL, H1
	lpm ST34, Z
	mov H1, ST41
	mov ZL, ST44
	lpm ST41, Z
	mov ZL, ST43
	lpm ST44, Z
	mov ZL, ST42
	lpm ST43, Z
	mov ZL, H1
	lpm ST42, Z
encryp1:ld H1, Y+
	eor ST11, H1
	ld H1, Y+
	eor ST21, H1
	ld H1, Y+
	eor ST31, H1
	ld H1, Y+
	eor ST41, H1
	ld H1, Y+
	eor ST12, H1
	ld H1, Y+
	eor ST22, H1
	ld H1, Y+
	eor ST32, H1
	ld H1, Y+
	eor ST42, H1
	ld H1, Y+
	eor ST13, H1
	ld H1, Y+
	eor ST23, H1
	ld H1, Y+
	eor ST33, H1
	ld H1, Y+
	eor ST43, H1
	ld H1, Y+
	eor ST14, H1
	ld H1, Y+
	eor ST24, H1
	ld H1, Y+
	eor ST34, H1
	ld H1, Y+
	eor ST44, H1
	ret

;;; ***************************************************************************
;;; 
;;; DECRYPT 
;;; This routine decrypts a 128 bit ciphertext block (given in ST11-ST44), 
;;; using an expanded (and patched) key supplied in the 16*11 memory locations 
;;; BEFORE YH:YL (YH:YL points behind the last byte of key material!). The 
;;; resulting 128 bit plaintext block is stored in ST11-ST44. The "equivalent 
;;; decryption algorithm" of Rijndael is implemented, so the MixColumns 
;;; diffusion operator has to be applied to the expanded key (done 
;;; with the routine patch_decryption_key) before calling the "decrypt" 
;;; routine.
;;;
;;; Parameters:
;;;         YH:YL:	pointer behind patched key
;;;         ST11-ST44:  128 bit ciphertext block
;;; Touched registers:
;;;     ST11-ST41,H1-H5,I,ZH,ZL,YH,YL
;;; Clock cycles:	3411
	
decrypt:rcall decryp1
	ldi I, 8
	ldi ZH, hi8(isbox0e)
decryp0:sbiw YL, 16
	mov ZL, ST11		; 1
	lpm H1, Z
	ldi ZH, hi8(isbox09)
	lpm H2, Z
	ldi ZH, hi8(isbox0d)
	lpm H3, Z
	ldi ZH, hi8(isbox0b)
	lpm H4, Z
	mov ZL, ST24
	lpm H5, Z
	eor H1, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H4, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H3, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor H2, H5
	mov ZL, ST33
	lpm H5, Z
	eor H3, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H4, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H1, H5
	ldi ZH, hi8(isbox0b)
	lpm H5, Z
	eor H2, H5
	mov ZL, ST42
	lpm H5, Z
	eor H3, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H2, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H1, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor H4, H5
	ldd ST11, Y+0
	eor ST11, H1
	mov ZL, ST21		; 2
	ldd ST21, Y+1
	eor ST21, H2
	lpm H2, Z
	ldi ZH, hi8(isbox09)
	lpm ST24, Z
	ldi ZH, hi8(isbox0d)
	lpm ST33, Z
	ldi ZH, hi8(isbox0b)
	lpm H1, Z
	mov ZL, ST12
	lpm H5, Z
	eor ST33, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor ST24, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H2, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor H1, H5
	mov ZL, ST34
	lpm H5, Z
	eor ST24, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor ST33, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H1, H5
	ldi ZH, hi8(isbox0b)
	lpm H5, Z
	eor H2, H5
	mov ZL, ST43
	lpm H5, Z
	eor ST24, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H2, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H1, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor ST33, H5
	ldd ST12, Y+4
	eor ST12, H1
	ldd ST42, Y+7
	eor ST42, ST33
	mov ZL, ST31		; 3
	ldd ST31, Y+2
	eor ST31, H3
	lpm ST34, Z
	ldi ZH, hi8(isbox09)
	lpm H3, Z
	ldi ZH, hi8(isbox0d)
	lpm H1, Z
	ldi ZH, hi8(isbox0b)
	lpm ST33, Z
	mov ZL, ST13
	lpm H5, Z
	eor H3, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor ST34, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor ST33, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor H1, H5
	mov ZL, ST22
	lpm H5, Z
	eor ST33, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor ST34, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H3, H5
	ldi ZH, hi8(isbox0b)
	lpm H5, Z
	eor H1, H5
	mov ZL, ST44
	lpm H5, Z
	eor ST34, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor ST33, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H1, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor H3, H5
	ldd ST13, Y+8
	eor ST13, H1
	ldd ST43, Y+11
	eor ST43, H3
	ldd ST22, Y+5
	eor ST22, H2
	mov ZL, ST41		; 4
	ldd ST41, Y+3
	eor ST41, H4
	lpm H4, Z
	ldi ZH, hi8(isbox09)
	lpm H1, Z
	ldi ZH, hi8(isbox0d)
	lpm H2, Z
	ldi ZH, hi8(isbox0b)
	lpm H3, Z
	mov ZL, ST14
	lpm H5, Z
	eor H4, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H3, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H2, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor H1, H5
	mov ZL, ST23
	lpm H5, Z
	eor H2, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H3, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H4, H5
	ldi ZH, hi8(isbox0b)
	lpm H5, Z
	eor H1, H5
	mov ZL, ST32
	lpm H5, Z
	eor H2, H5
	ldi ZH, hi8(isbox0d)
	lpm H5, Z
	eor H1, H5
	ldi ZH, hi8(isbox09)
	lpm H5, Z
	eor H4, H5
	ldi ZH, hi8(isbox0e)
	lpm H5, Z
	eor H3, H5
	ldd ST14, Y+12
	eor ST14, H1
	ldd ST23, Y+9
	eor ST23, ST33
	ldd ST32, Y+6
	eor ST32, ST24
	ldd ST33, Y+10
	eor ST33, ST34
	ldd ST34, Y+14
	eor ST34, H3
	ldd ST44, Y+15
	eor ST44, H4
	ldd ST24, Y+13
	eor ST24, H2
	dec I
	sbrs I,7
	jmp decryp0	
	ldi ZH, hi8(isbox)
	mov ZL, ST11
	lpm ST11, Z
	mov ZL, ST12
	lpm ST12, Z
	mov ZL, ST13
	lpm ST13, Z
	mov ZL, ST14
	lpm ST14, Z
	mov H1, ST24
	mov ZL, ST23
	lpm ST24, Z
	mov ZL, ST22
	lpm ST23, Z
	mov ZL, ST21
	lpm ST22, Z
	mov ZL, H1
	lpm ST21, Z
	mov H1, ST31
	mov ZL, ST33
	lpm ST31, Z
	mov ZL, H1
	lpm ST33, Z
	mov H1, ST32
	mov ZL, ST34
	lpm ST32, Z
	mov ZL, H1
	lpm ST34, Z
	mov H1, ST41
	mov ZL, ST42
	lpm ST41, Z
	mov ZL, ST43
	lpm ST42, Z
	mov ZL, ST44
	lpm ST43, Z
	mov ZL, H1
	lpm ST44, Z
decryp1:ld H1, -Y
	eor ST44, H1
	ld H1, -Y
	eor ST34, H1
	ld H1, -Y
	eor ST24, H1
	ld H1, -Y
	eor ST14, H1
	ld H1, -Y
	eor ST43, H1
	ld H1, -Y
	eor ST33, H1
	ld H1, -Y
	eor ST23, H1
	ld H1, -Y
	eor ST13, H1
	ld H1, -Y
	eor ST42, H1
	ld H1, -Y
	eor ST32, H1
	ld H1, -Y
	eor ST22, H1
	ld H1, -Y
	eor ST12, H1
	ld H1, -Y
	eor ST41, H1
	ld H1, -Y
	eor ST31, H1
	ld H1, -Y
	eor ST21, H1
	ld H1, -Y
	eor ST11, H1
	ret
	


;;; ***************************************************************************
;;; 
;;; S-BOX
;;; Rijndael consists of a non-linear step in its rounds (called "sbox step"), 
;;; generally implemented with hard-coded lookup tables. The implementation 
;;; given above makes use of seven lookup tables in total: the sbox itself, 
;;; its inverse, and scaled versions of both (e.g. sbox02[] = 2*sbox[]).
;;;
;;; This generous employment of expensive space of flash memory has two
;;; important advantages: excellent performance and protection against 
;;; timing and power measurement attacks.
;;;
;;; The seven tables have to be aligned to a flash position with its lower 
;;; address byte equal to $00. In assembler syntax: lo8(sbox) == 0.
;;; To ensure the proper alignment of the sboxes, the assembler directive
;;; .ORG is used (below the sboxes are defined to begin at $800). Note, that 
;;; any other address can be used as well, as long as the lower byte is equal 
;;; to $00.
;;;
;;; The order of the sboxes is totally arbitrary. They even do not have to be
;;; allocated in adjacent memory areas.
	
.section .text
.balign 256

.global sbox	
sbox:
.byte 0x63,0x7c,0x77,0x7b,0xf2,0x6b,0x6f,0xc5,0x30,0x01,0x67,0x2b,0xfe,0xd7,0xab,0x76 
.byte 0xca,0x82,0xc9,0x7d,0xfa,0x59,0x47,0xf0,0xad,0xd4,0xa2,0xaf,0x9c,0xa4,0x72,0xc0 
.byte 0xb7,0xfd,0x93,0x26,0x36,0x3f,0xf7,0xcc,0x34,0xa5,0xe5,0xf1,0x71,0xd8,0x31,0x15 
.byte 0x04,0xc7,0x23,0xc3,0x18,0x96,0x05,0x9a,0x07,0x12,0x80,0xe2,0xeb,0x27,0xb2,0x75 
.byte 0x09,0x83,0x2c,0x1a,0x1b,0x6e,0x5a,0xa0,0x52,0x3b,0xd6,0xb3,0x29,0xe3,0x2f,0x84 
.byte 0x53,0xd1,0x00,0xed,0x20,0xfc,0xb1,0x5b,0x6a,0xcb,0xbe,0x39,0x4a,0x4c,0x58,0xcf 
.byte 0xd0,0xef,0xaa,0xfb,0x43,0x4d,0x33,0x85,0x45,0xf9,0x02,0x7f,0x50,0x3c,0x9f,0xa8 
.byte 0x51,0xa3,0x40,0x8f,0x92,0x9d,0x38,0xf5,0xbc,0xb6,0xda,0x21,0x10,0xff,0xf3,0xd2 
.byte 0xcd,0x0c,0x13,0xec,0x5f,0x97,0x44,0x17,0xc4,0xa7,0x7e,0x3d,0x64,0x5d,0x19,0x73 
.byte 0x60,0x81,0x4f,0xdc,0x22,0x2a,0x90,0x88,0x46,0xee,0xb8,0x14,0xde,0x5e,0x0b,0xdb 
.byte 0xe0,0x32,0x3a,0x0a,0x49,0x06,0x24,0x5c,0xc2,0xd3,0xac,0x62,0x91,0x95,0xe4,0x79 
.byte 0xe7,0xc8,0x37,0x6d,0x8d,0xd5,0x4e,0xa9,0x6c,0x56,0xf4,0xea,0x65,0x7a,0xae,0x08 
.byte 0xba,0x78,0x25,0x2e,0x1c,0xa6,0xb4,0xc6,0xe8,0xdd,0x74,0x1f,0x4b,0xbd,0x8b,0x8a 
.byte 0x70,0x3e,0xb5,0x66,0x48,0x03,0xf6,0x0e,0x61,0x35,0x57,0xb9,0x86,0xc1,0x1d,0x9e 
.byte 0xe1,0xf8,0x98,0x11,0x69,0xd9,0x8e,0x94,0x9b,0x1e,0x87,0xe9,0xce,0x55,0x28,0xdf 
.byte 0x8c,0xa1,0x89,0x0d,0xbf,0xe6,0x42,0x68,0x41,0x99,0x2d,0x0f,0xb0,0x54,0xbb,0x16 

sbox02:
.byte 0xc6,0xf8,0xee,0xf6,0xff,0xd6,0xde,0x91,0x60,0x02,0xce,0x56,0xe7,0xb5,0x4d,0xec 
.byte 0x8f,0x1f,0x89,0xfa,0xef,0xb2,0x8e,0xfb,0x41,0xb3,0x5f,0x45,0x23,0x53,0xe4,0x9b 
.byte 0x75,0xe1,0x3d,0x4c,0x6c,0x7e,0xf5,0x83,0x68,0x51,0xd1,0xf9,0xe2,0xab,0x62,0x2a 
.byte 0x08,0x95,0x46,0x9d,0x30,0x37,0x0a,0x2f,0x0e,0x24,0x1b,0xdf,0xcd,0x4e,0x7f,0xea 
.byte 0x12,0x1d,0x58,0x34,0x36,0xdc,0xb4,0x5b,0xa4,0x76,0xb7,0x7d,0x52,0xdd,0x5e,0x13 
.byte 0xa6,0xb9,0x00,0xc1,0x40,0xe3,0x79,0xb6,0xd4,0x8d,0x67,0x72,0x94,0x98,0xb0,0x85 
.byte 0xbb,0xc5,0x4f,0xed,0x86,0x9a,0x66,0x11,0x8a,0xe9,0x04,0xfe,0xa0,0x78,0x25,0x4b 
.byte 0xa2,0x5d,0x80,0x05,0x3f,0x21,0x70,0xf1,0x63,0x77,0xaf,0x42,0x20,0xe5,0xfd,0xbf 
.byte 0x81,0x18,0x26,0xc3,0xbe,0x35,0x88,0x2e,0x93,0x55,0xfc,0x7a,0xc8,0xba,0x32,0xe6 
.byte 0xc0,0x19,0x9e,0xa3,0x44,0x54,0x3b,0x0b,0x8c,0xc7,0x6b,0x28,0xa7,0xbc,0x16,0xad 
.byte 0xdb,0x64,0x74,0x14,0x92,0x0c,0x48,0xb8,0x9f,0xbd,0x43,0xc4,0x39,0x31,0xd3,0xf2 
.byte 0xd5,0x8b,0x6e,0xda,0x01,0xb1,0x9c,0x49,0xd8,0xac,0xf3,0xcf,0xca,0xf4,0x47,0x10 
.byte 0x6f,0xf0,0x4a,0x5c,0x38,0x57,0x73,0x97,0xcb,0xa1,0xe8,0x3e,0x96,0x61,0x0d,0x0f 
.byte 0xe0,0x7c,0x71,0xcc,0x90,0x06,0xf7,0x1c,0xc2,0x6a,0xae,0x69,0x17,0x99,0x3a,0x27 
.byte 0xd9,0xeb,0x2b,0x22,0xd2,0xa9,0x07,0x33,0x2d,0x3c,0x15,0xc9,0x87,0xaa,0x50,0xa5 
.byte 0x03,0x59,0x09,0x1a,0x65,0xd7,0x84,0xd0,0x82,0x29,0x5a,0x1e,0x7b,0xa8,0x6d,0x2c 

isbox:
.byte 0x52,0x09,0x6a,0xd5,0x30,0x36,0xa5,0x38,0xbf,0x40,0xa3,0x9e,0x81,0xf3,0xd7,0xfb 
.byte 0x7c,0xe3,0x39,0x82,0x9b,0x2f,0xff,0x87,0x34,0x8e,0x43,0x44,0xc4,0xde,0xe9,0xcb 
.byte 0x54,0x7b,0x94,0x32,0xa6,0xc2,0x23,0x3d,0xee,0x4c,0x95,0x0b,0x42,0xfa,0xc3,0x4e 
.byte 0x08,0x2e,0xa1,0x66,0x28,0xd9,0x24,0xb2,0x76,0x5b,0xa2,0x49,0x6d,0x8b,0xd1,0x25 
.byte 0x72,0xf8,0xf6,0x64,0x86,0x68,0x98,0x16,0xd4,0xa4,0x5c,0xcc,0x5d,0x65,0xb6,0x92 
.byte 0x6c,0x70,0x48,0x50,0xfd,0xed,0xb9,0xda,0x5e,0x15,0x46,0x57,0xa7,0x8d,0x9d,0x84 
.byte 0x90,0xd8,0xab,0x00,0x8c,0xbc,0xd3,0x0a,0xf7,0xe4,0x58,0x05,0xb8,0xb3,0x45,0x06 
.byte 0xd0,0x2c,0x1e,0x8f,0xca,0x3f,0x0f,0x02,0xc1,0xaf,0xbd,0x03,0x01,0x13,0x8a,0x6b 
.byte 0x3a,0x91,0x11,0x41,0x4f,0x67,0xdc,0xea,0x97,0xf2,0xcf,0xce,0xf0,0xb4,0xe6,0x73 
.byte 0x96,0xac,0x74,0x22,0xe7,0xad,0x35,0x85,0xe2,0xf9,0x37,0xe8,0x1c,0x75,0xdf,0x6e 
.byte 0x47,0xf1,0x1a,0x71,0x1d,0x29,0xc5,0x89,0x6f,0xb7,0x62,0x0e,0xaa,0x18,0xbe,0x1b 
.byte 0xfc,0x56,0x3e,0x4b,0xc6,0xd2,0x79,0x20,0x9a,0xdb,0xc0,0xfe,0x78,0xcd,0x5a,0xf4 
.byte 0x1f,0xdd,0xa8,0x33,0x88,0x07,0xc7,0x31,0xb1,0x12,0x10,0x59,0x27,0x80,0xec,0x5f 
.byte 0x60,0x51,0x7f,0xa9,0x19,0xb5,0x4a,0x0d,0x2d,0xe5,0x7a,0x9f,0x93,0xc9,0x9c,0xef 
.byte 0xa0,0xe0,0x3b,0x4d,0xae,0x2a,0xf5,0xb0,0xc8,0xeb,0xbb,0x3c,0x83,0x53,0x99,0x61 
.byte 0x17,0x2b,0x04,0x7e,0xba,0x77,0xd6,0x26,0xe1,0x69,0x14,0x63,0x55,0x21,0x0c,0x7d 

isbox0e:
.byte 0x51,0x7e,0x1a,0x3a,0x3b,0x1f,0xac,0x4b,0x20,0xad,0x88,0xf5,0x4f,0xc5,0x26,0xb5 
.byte 0xde,0x25,0x45,0x5d,0xc3,0x81,0x8d,0x6b,0x03,0x15,0xbf,0x95,0xd4,0x58,0x49,0x8e 
.byte 0x75,0xf4,0x99,0x27,0xbe,0xf0,0xc9,0x7d,0x63,0xe5,0x97,0x62,0xb1,0xbb,0xfe,0xf9 
.byte 0x70,0x8f,0x94,0x52,0xab,0x72,0xe3,0x66,0xb2,0x2f,0x86,0xd3,0x30,0x23,0x02,0xed 
.byte 0x8a,0xa7,0xf3,0x4e,0x65,0x06,0xd1,0xc4,0x34,0xa2,0x05,0xa4,0x0b,0x40,0x5e,0xbd 
.byte 0x3e,0x96,0xdd,0x4d,0x91,0x71,0x04,0x60,0x19,0xd6,0x89,0x67,0xb0,0x07,0xe7,0x79 
.byte 0xa1,0x7c,0xf8,0x00,0x09,0x32,0x1e,0x6c,0xfd,0x0f,0x3d,0x36,0x0a,0x68,0x9b,0x24 
.byte 0x0c,0x93,0xb4,0x1b,0x80,0x61,0x5a,0x1c,0xe2,0xc0,0x3c,0x12,0x0e,0xf2,0x2d,0x14 
.byte 0x57,0xaf,0xee,0xa3,0xf7,0x5c,0x44,0x5b,0x8b,0xcb,0xb6,0xb8,0xd7,0x42,0x13,0x84 
.byte 0x85,0xd2,0xae,0xc7,0x1d,0xdc,0x0d,0x77,0x2b,0xa9,0x11,0x47,0xa8,0xa0,0x56,0x22 
.byte 0x87,0xd9,0x8c,0x98,0xa6,0xa5,0xda,0x3f,0x2c,0x50,0x6a,0x54,0xf6,0x90,0x2e,0x82 
.byte 0x9f,0x69,0x6f,0xcf,0xc8,0x10,0xe8,0xdb,0xcd,0x6e,0xec,0x83,0xe6,0xaa,0x21,0xef 
.byte 0xba,0x4a,0xea,0x29,0x31,0x2a,0xc6,0x35,0x74,0xfc,0xe0,0x33,0xf1,0x41,0x7f,0x17 
.byte 0x76,0x43,0xcc,0xe4,0x9e,0x4c,0xc1,0x46,0x9d,0x01,0xfa,0xfb,0xb3,0x92,0xe9,0x6d 
.byte 0x9a,0x37,0x59,0xeb,0xce,0xb7,0xe1,0x7a,0x9c,0x55,0x18,0x73,0x53,0x5f,0xdf,0x78 
.byte 0xca,0xb9,0x38,0xc2,0x16,0xbc,0x28,0xff,0x39,0x08,0xd8,0x64,0x7b,0xd5,0x48,0xd0 

isbox09:
.byte 0xf4,0x41,0x17,0x27,0xab,0x9d,0xfa,0xe3,0x30,0x76,0xcc,0x02,0xe5,0x2a,0x35,0x62 
.byte 0xb1,0xba,0xea,0xfe,0x2f,0x4c,0x46,0xd3,0x8f,0x92,0x6d,0x52,0xbe,0x74,0xe0,0xc9 
.byte 0xc2,0x8e,0x58,0xb9,0xe1,0x88,0x20,0xce,0xdf,0x1a,0x51,0x53,0x64,0x6b,0x81,0x08 
.byte 0x48,0x45,0xde,0x7b,0x73,0x4b,0x1f,0x55,0xeb,0xb5,0xc5,0x37,0x28,0xbf,0x03,0x16 
.byte 0xcf,0x79,0x07,0x69,0xda,0x05,0x34,0xa6,0x2e,0xf3,0x8a,0xf6,0x83,0x60,0x71,0x6e 
.byte 0x21,0xdd,0x3e,0xe6,0x54,0xc4,0x06,0x50,0x98,0xbd,0x40,0xd9,0xe8,0x89,0x19,0xc8 
.byte 0x7c,0x42,0x84,0x00,0x80,0x2b,0x11,0x5a,0x0e,0x85,0xae,0x2d,0x0f,0x5c,0x5b,0x36 
.byte 0x0a,0x57,0xee,0x9b,0xc0,0xdc,0x77,0x12,0x93,0xa0,0x22,0x1b,0x09,0x8b,0xb6,0x1e 
.byte 0xf1,0x75,0x99,0x7f,0x01,0x72,0x66,0xfb,0x43,0x23,0xed,0xe4,0x31,0x63,0x97,0xc6 
.byte 0x4a,0xbb,0xf9,0x29,0x9e,0xb2,0x86,0xc1,0xb3,0x70,0x94,0xe9,0xfc,0xf0,0x7d,0x33 
.byte 0x49,0x38,0xca,0xd4,0xf5,0x7a,0xb7,0xad,0x3a,0x78,0x5f,0x7e,0x8d,0xd8,0x39,0xc3 
.byte 0x5d,0xd0,0xd5,0x25,0xac,0x18,0x9c,0x3b,0x26,0x59,0x9a,0x4f,0x95,0xff,0xbc,0x15 
.byte 0xe7,0x6f,0x9f,0xb0,0xa4,0x3f,0xa5,0xa2,0x4e,0x82,0x90,0xa7,0x04,0xec,0xcd,0x91 
.byte 0x4d,0xef,0xaa,0x96,0xd1,0x6a,0x2c,0x65,0x5e,0x8c,0x87,0x0b,0x67,0xdb,0x10,0xd6 
.byte 0xd7,0xa1,0xf8,0x13,0xa9,0x61,0x1c,0x47,0xd2,0xf2,0x14,0xc7,0xf7,0xfd,0x3d,0x44 
.byte 0xaf,0x68,0x24,0xa3,0x1d,0xe2,0x3c,0x0d,0xa8,0x0c,0xb4,0x56,0xcb,0x32,0x6c,0xb8 

isbox0d:
.byte 0xa7,0x65,0xa4,0x5e,0x6b,0x45,0x58,0x03,0xfa,0x6d,0x76,0x4c,0xd7,0xcb,0x44,0xa3 
.byte 0x5a,0x1b,0x0e,0xc0,0x75,0xf0,0x97,0xf9,0x5f,0x9c,0x7a,0x59,0x83,0x21,0x69,0xc8 
.byte 0x89,0x79,0x3e,0x71,0x4f,0xad,0xac,0x3a,0x4a,0x31,0x33,0x7f,0x77,0xae,0xa0,0x2b 
.byte 0x68,0xfd,0x6c,0xf8,0xd3,0x02,0x8f,0xab,0x28,0xc2,0x7b,0x08,0x87,0xa5,0x6a,0x82 
.byte 0x1c,0xb4,0xf2,0xe2,0xf4,0xbe,0x62,0xfe,0x53,0x55,0xe1,0xeb,0xec,0xef,0x9f,0x10 
.byte 0x8a,0x06,0x05,0xbd,0x8d,0x5d,0xd4,0x15,0xfb,0xe9,0x43,0x9e,0x42,0x8b,0x5b,0xee 
.byte 0x0a,0x0f,0x1e,0x00,0x86,0xed,0x70,0x72,0xff,0x38,0xd5,0x39,0xd9,0xa6,0x54,0x2e 
.byte 0x67,0xe7,0x96,0x91,0xc5,0x20,0x4b,0x1a,0xba,0x2a,0xe0,0x17,0x0d,0xc7,0xa8,0xa9 
.byte 0x19,0x07,0xdd,0x60,0x26,0xf5,0x3b,0x7e,0x29,0xc6,0xfc,0xf1,0xdc,0x85,0x22,0x11 
.byte 0x24,0x3d,0x32,0xa1,0x2f,0x30,0x52,0xe3,0x16,0xb9,0x48,0x64,0x8c,0x3f,0x2c,0x90 
.byte 0x4e,0xd1,0xa2,0x0b,0x81,0xde,0x8e,0xbf,0x9d,0x92,0xcc,0x46,0x13,0xb8,0xf7,0xaf 
.byte 0x80,0x93,0x2d,0x12,0x99,0x7d,0x63,0xbb,0x78,0x18,0xb7,0x9a,0x6e,0xe6,0xcf,0xe8 
.byte 0x9b,0x36,0x09,0x7c,0xb2,0x23,0x94,0x66,0xbc,0xca,0xd0,0xd8,0x98,0xda,0x50,0xf6 
.byte 0xd6,0xb0,0x4d,0x04,0xb5,0x88,0x1f,0x51,0xea,0x35,0x74,0x41,0x1d,0xd2,0x56,0x47 
.byte 0x61,0x0c,0x14,0x3c,0x27,0xc9,0xe5,0xb1,0xdf,0x73,0xce,0x37,0xcd,0xaa,0x6f,0xdb 
.byte 0xf3,0xc4,0x34,0x40,0xc3,0x25,0x49,0x95,0x01,0xb3,0xe4,0xc1,0x84,0xb6,0x5c,0x57 

isbox0b:
.byte 0x50,0x53,0xc3,0x96,0xcb,0xf1,0xab,0x93,0x55,0xf6,0x91,0x25,0xfc,0xd7,0x80,0x8f 
.byte 0x49,0x67,0x98,0xe1,0x02,0x12,0xa3,0xc6,0xe7,0x95,0xeb,0xda,0x2d,0xd3,0x29,0x44 
.byte 0x6a,0x78,0x6b,0xdd,0xb6,0x17,0x66,0xb4,0x18,0x82,0x60,0x45,0xe0,0x84,0x1c,0x94 
.byte 0x58,0x19,0x87,0xb7,0x23,0xe2,0x57,0x2a,0x07,0x03,0x9a,0xa5,0xf2,0xb2,0xba,0x5c 
.byte 0x2b,0x92,0xf0,0xa1,0xcd,0xd5,0x1f,0x8a,0x9d,0xa0,0x32,0x75,0x39,0xaa,0x06,0x51 
.byte 0xf9,0x3d,0xae,0x46,0xb5,0x05,0x6f,0xff,0x24,0x97,0xcc,0x77,0xbd,0x88,0x38,0xdb 
.byte 0x47,0xe9,0xc9,0x00,0x83,0x48,0xac,0x4e,0xfb,0x56,0x1e,0x27,0x64,0x21,0xd1,0x3a 
.byte 0xb1,0x0f,0xd2,0x9e,0x4f,0xa2,0x69,0x16,0x0a,0xe5,0x43,0x1d,0x0b,0xad,0xb9,0xc8 
.byte 0x85,0x4c,0xbb,0xfd,0x9f,0xbc,0xc5,0x34,0x76,0xdc,0x68,0x63,0xca,0x10,0x40,0x20 
.byte 0x7d,0xf8,0x11,0x6d,0x4b,0xf3,0xec,0xd0,0x6c,0x99,0xfa,0x22,0xc4,0x1a,0xd8,0xef 
.byte 0xc7,0xc1,0xfe,0x36,0xcf,0x28,0x26,0xa4,0xe4,0x0d,0x9b,0x62,0xc2,0xe8,0x5e,0xf5 
.byte 0xbe,0x7c,0xa9,0xb3,0x3b,0xa7,0x6e,0x7b,0x09,0xf4,0x01,0xa8,0x65,0x7e,0x08,0xe6 
.byte 0xd9,0xce,0xd4,0xd6,0xaf,0x31,0x30,0xc0,0x37,0xa6,0xb0,0x15,0x4a,0xf7,0x0e,0x2f 
.byte 0x8d,0x4d,0x54,0xdf,0xe3,0x1b,0xb8,0x7f,0x04,0x5d,0x73,0x2e,0x5a,0x52,0x33,0x13 
.byte 0x8c,0x7a,0x8e,0x89,0xee,0x35,0xed,0x3c,0x59,0x3f,0x79,0xbf,0xea,0x5b,0x14,0x86 
.byte 0x81,0x3e,0x2c,0x5f,0x72,0x0c,0x8b,0x41,0x71,0xde,0x9c,0x90,0x61,0x70,0x74,0x42 
